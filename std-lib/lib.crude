define id(x) {
  return x;
}

define add(a, b) {
  return a + b;
}

define isOdd(x) {
  return x % 2 != 0;
}

define isEven(x) {
  return x % 2 == 0;
}

define inc(x) {
  return x + 1;
}

define dec(x) {
  return x - 1;
}

define filter(predicate, arr) {
  let filtArr = [];
  for(i, 0, length(arr)) {
    if(predicate(arr[i])) {
      change(filtArr, length(filtArr), arr[i]);
    }
  }
  return filtArr;
}

define map(fn, arr) {
  let newArr = [];
  for(i, 0, length(arr)) {
    change(newArr, i, fn(arr[i]));
  }
  return newArr;
}

define every(predicate, li) {
  for(i, 0, length(li)) {
    if(predicate(li[i]) == false) {
      return false;
    }
  }
  return true;
}

define some(predicate, li) {
  let isSomeTrue = false;
  for(i, 0, length(li)) {
    if(predicate(li[i]) == true) {
      set isSomeTrue = true;
    }
  }
  return isSomeTrue;
}

define foldl(fn, curr, li) {
  define loop(i, currentValue) {
    if(i < length(li)) {
      return loop(i + 1, fn(currentValue, li[i]));
    }
    return currentValue;
  }
  return loop(0, curr);
}

define trimStart(s) {
  define count(n) {
    if(slice(s, n, n + 1)!= " ") {
      let len = length(s);
      return slice(s, n, len);
    }
    return count(n + 1);
  }
  return count(0);
}


define sort(arr) {
  for(i, 0, length(arr)) {
    let min = i;
    for(j, i + 1, length(arr)) {
      let a = arr[j];
      let b = arr[min];
      if (a < b) {
        set min = j;
      }
    }
    if(min != i) {
      let temp = arr[i];
      set arr = change(arr, i, arr[min]);
      set arr = change(arr, min, temp);
    }
  }
  return arr;
}


define head(arr) {
  return arr[0];
}

define last(arr) {
  return arr[length(arr) - 1];
}

define tail(arr) {
  return slice(arr, 1, length(arr));
}

define addToHead(arr, el) {
  return concat(el, arr);
}

define addToTail(arr, el) {
  return concat(arr, el);
}

define compose(fns, val) {
  if(length(fns) == 0) {
    return val;
  }
  let fn = head(fns);
  return compose(tail(fns), fn(val));
}

define includes(arr, el) {
  define loopIsElInArray(i, condIsTrue) {
    if(i < length(arr)) {
      let currEl = arr[i];
      if(currEl == el) {
        return true;
      }
      return loopIsElInArray(i + 1, condIsTrue);
    }
    return condIsTrue;
  }
  return loopIsElInArray(0, false);
}

define findIndex(arr, el) {
  define loopIsElInArray(i, condIsFalse) {
    if(i < length(arr)) {
      let currEl = arr[i];
      if(currEl == el) {
        return i;
      }
      return loopIsElInArray(i + 1, condIsFalse);
    }
    return condIsFalse;
  }
  return loopIsElInArray(0, -1);
}

define reverse(arr) {
  let newArr = [];
  let len = length(arr);
  for(i, 0, len) {
    change(newArr, i, arr[len - i - 1]);
  }
  return newArr;
}
